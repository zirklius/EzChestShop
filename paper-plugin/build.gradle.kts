plugins {
    `java-library`
    id("com.gradleup.shadow") version "9.6.0"
    id("com.gorylenko.gradle-git-properties") version "4.0.1"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(25)
}

dependencies {
    implementation(project(":core"))
    implementation(project(":internal:v26_2"))
}

gitProperties {
    gitPropertiesName = "version.properties"
    dotGitDirectory = project.rootProject.layout.projectDirectory.dir(".git")
    keys = listOf("git.branch", "git.build.version", "git.commit.id.abbrev")
}

tasks {
    shadowJar {
        archiveFileName = "${rootProject.name}-${version}.jar"

        dependencies {
            exclude(dependency("com.google.code.gson:gson"))
            exclude(dependency("org.slf4j:slf4j-api"))
        }

        manifest {
            attributes["paperweight-mappings-namespace"] = "mojang"
        }

        exclude("META-INF/**")
        exclude("net/kyori/**")

        relocate("com.github.Anon8281.universalScheduler", "me.deadlight.ezchestshop.thirdparty.scheduler")
        relocate("dev.triumphteam.gui", "me.deadlight.ezchestshop.thirdparty.triumphgui")
        relocate("de.themoep.minedown", "me.deadlight.ezchestshop.thirdparty.minedown")
        relocate("com.zaxxer.hikari", "me.deadlight.ezchestshop.thirdparty.hikari")
        relocate("org.bstats", "me.deadlight.ezchestshop.thirdparty.bstats")
    }

    assemble {
        dependsOn(shadowJar)
    }
}